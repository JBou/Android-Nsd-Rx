apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

static boolean isTag() {
    def tag = System.getenv('TRAVIS_TAG')
    return !(tag == null || tag.isEmpty())
}

String getVersionString() {
    String buildNumber = System.getenv('TRAVIS_BUILD_NUMBER') ?: "0"
    String version = project.property('pom.version').replaceAll(".(0)\$", "." + buildNumber)
    return isTag() ? version : version + "-SNAPSHOT"
}

group = 'com.toxicbakery.library.nsd.rx'
version = getVersionString()

repositories {
    google()
    jcenter()
    mavenCentral()
}

android {
    compileSdkVersion 27
    buildToolsVersion = "27.0.1"
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        test {
            java.srcDir 'src/sharedTest/java'
        }
        androidTest {
            java.srcDir 'src/sharedTest/java'
        }
    }
}

dependencies {
    androidTestImplementation 'com.android.support.test:runner:1.0.1'

    def mockitoVersion = "2.7.22"
    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"

    testImplementation 'junit:junit:4.12'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    implementation "io.reactivex.rxjava2:rxjava:2.1.6"
    implementation "com.android.support:support-annotations:27.0.1"
}

apply from: 'publish.gradle'