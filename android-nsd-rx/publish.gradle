apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/${project.name}-release.aar")
            groupId group
            artifactId 'android-nsd-rx'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project["bintrayUser"] : System.getenv('bintray_user')
    key = project.hasProperty('bintrayApiKey') ? project["bintrayApiKey"] : System.getenv('bintray_key')
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'AndroidNsdRx'
        name = 'android-nsd-rx'
        description = "An Rx wrapper around the Android NsdManager api."
        publish = true
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/ToxicBakery/Android-Nsd-Rx.git'
        dryRun = false
        version {
            name = this.version
            released = new Date()
            vcsTag = this.version
        }
    }
}